<!--
FileName:       index.html
Programmer:     Dan Cliburn, Dean S, Chris C, Chris S
Date:           10/03/2020
Purpose:		This file defines the html code necessary to run our WebGL 2 program.
I've made an attempt to separate the parts of the program using the Model-View-Controller 
design pattern. The model.js, view.js, and controller.js files define the code for those 
components respectively. I define the vertex and fragment shader code in this file.
-->
<html>

<head>
    <title>COMP 153 - Assignment 3</title>

    <style type="text/css">
        canvas {
            border: 5px dotted blue;
        }
    </style>

    <!-- load the javascript files -->
    <script src="model.js" defer></script>
    <script src="view.js" defer></script>
    <script src="controller.js" defer></script>

    <!-- Define the vertex and fragment shaders -->
    <script id="vertex-shader" type="shader.vert">
        #version 300 es //specifies that we want OpenGL ES 3.0
        //This vertex shader is based on Examples 7.8 on pages 377 and 378 of the OpenGL Programming
        //Guide (8th edition) with some tweaks to support shininess as a vertex property. I also
        //modified the code somewhat to make it closer to the Phong Reflection Model discussed in class.

        layout(location = 0) in vec4 vertexPosition;
        layout(location = 1) in vec4 vertexColor;
        layout(location = 2) in vec3 vertexNormal;
        layout(location = 3) in float vertexShininess;

        out vec4 position; //position of the vertex in "eye" coordinates
        out vec4 color;
        out vec3 normal; //orientation of the normal in "eye" coordinates
        out float shininess;

        void main()
        {
            //assign the in variables (attributes) to out variables (varyings)
            //so that they can be accessed in the fragment shader
	        position = vertexPosition;
	        color = vertexColor;
	        normal = normalize(vertexNormal); //normalize just in case it is not a unit vector
	        shininess = vertexShininess;

	        gl_Position = vertexPosition;
        }
    </script>

    <script id="fragment-shader" type="shader.frag">
        #version 300 es //specifies that we want OpenGL ES 3.0
        precision mediump float; //specifies "medium precision"
        //This fragment shader is based on Examples 7.8 on pages 377 and 378 of the OpenGL Programming
        //Guide (8th edition) with some tweaks to support shininess as a vertex property. I also
        //modified the code somewhat to make it closer to the Phong Reflection Model discussed in class.

        //uniform variables
        uniform vec3 globalAmbientLight;
        uniform vec4 light_color; //color of light
        uniform vec4 light_position; //location of light if w = 1, otherwise the direction towards the light
        //light attenuation coefficients (position.w must be 1 to use these)
        uniform float constantAttenuation;
        uniform float linearAttenuation;
        uniform float quadraticAttenuation;

        in vec4 position; //position of the fragment
        in vec4 color; //surface color at the fragment
        in vec3 normal; //normal vector at the fragment
        in float shininess; //shininess of the surface at the fragment

        out vec4 frag_color; //resulting fragment color after lighting is applied

        void main()
        {
	        //Implement the Phong reflectance model
	        //initialize scatteredLight to the global ambience * fragment color
	        vec3 scatteredLight = globalAmbientLight*color.rgb;
	        vec3 reflectedLight = vec3(0.0, 0.0, 0.0);
	        vec3 surfaceSpecularColor = vec3(1.0, 1.0, 1.0);//normally we want the specular color of the surface to be white
												     //so that the color of the specular highlight is the color of the light	
	        vec3 I = light_color.rgb;  //Intensity (color) of the light
	        vec3 L; //this will be the L vector in the Phong reflectance model
			
	        vec3 surfaceDiffuseColor = color.rgb; //k_dif in the Phong reflectance model
	        float f; //attenuation in the phong reflectance model
				
	        if (light_position.w > 0.1) //a positional light
	        {
		        vec4 LTemp = light_position - position; //Calculate the L vector
		        L = vec3(LTemp.x, LTemp.y, LTemp.z); //remove the w value from the L vector
		        float d = length(L); //d in the phong reflectance model
			    L = L / d; //normalize L

		        //Calculate the attenuation factor, f in the phong reflectance model
			    float attenuationDenominator =	constantAttenuation + linearAttenuation * d + quadraticAttenuation * d * d;
		        if (attenuationDenominator < 0.001) attenuationDenominator = 1.0; //make sure we don't divide by 0
			    f = 1.0 / attenuationDenominator;
		    }
		    else //a directional light
	        {
                //remove w value to form the L vector and normalize
		        L = normalize(vec3(light_position.x, light_position.y, light_position.z));
			    f = 1.0;  //no attenuation for directional lights
		    }

	        float diffuseModifier = max ( 0.0, dot(normal, L) );
		    float specularModifier;
	        if (diffuseModifier > 0.001) //light is shining directly on the object so there may be a specular highlight
            {
		        //Find the reflectance vector, r, and the vector towards the viewer, v
			    vec3 r = normalize( reflect (-L, normal) );  //find r by "reflecting" L about n
		        vec4 viewerPos = vec4(0.0, 0.0, 0.0, 1.0); //The viewer is at (0,0,0) in this example
			    vec4 vTemp = viewerPos - position;
			    vec3 v = normalize(vec3(vTemp.x, vTemp.y, vTemp.z)); //calculate v, the vector towards the viewer 
			    specularModifier = pow (max ( 0.0, dot(r, v)), shininess );
		    }
		    else //no specular highlight, light is probably behind the surface
		    {
		        specularModifier = 0.0;
		    }

		    //Accumulate all the light's effects
		    reflectedLight += f *		                                                //attenuation
							     (  (I * surfaceDiffuseColor * diffuseModifier) +		//diffuse term
							        (I * surfaceSpecularColor * specularModifier) );	//specular term

	        vec3 sumOfLights = scatteredLight + reflectedLight;
	        vec3 rgb = min ( sumOfLights, vec3(1.0, 1.0, 1.0) );  //clamp lighting at all white
            frag_color = vec4(rgb.r, rgb.g, rgb.b, color.a);  //use the fragment's original alpha
        }
    </script>

    <!-- Call our controller function from main once the document has loaded -->
    <script type="text/javascript">
        function main() {
            controller(); //controller is defined in controller.js
        }
        window.onload = main; 
    </script>
</head>

<body>
    <canvas id="webgl-canvas" width="600" height="600">
        Your browser does not support the HTML5 canvas element.
    </canvas>
</body>

</html>